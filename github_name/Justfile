_default:
  @just --list --unsorted

tool := "github_name"
docker_container_registry := "ghcr.io"
docker_user_repo := "mihaigalos"
docker_image_version := "0.0.1"
docker_image := docker_container_registry + "/" + docker_user_repo + "/" + tool+ ":" + docker_image_version
docker_image_dockerhub := docker_user_repo + "/" + tool+ ":" + docker_image_version

run +args="":
    docker run --rm -it {{ docker_image_dockerhub }} {{ args }}
    
build +args="":
    docker build -t {{ docker_image_dockerhub }} {{ args }} .

push:
    docker push {{ docker_image_dockerhub }}

test:
    #!/bin/bash
    ok() { echo -e "\e[1;32mOK\e[0m"; }
    err() { echo -e "\e[1;31m${@}\e[0m" >&2; exit 1; }
    result=$(just run https://github.com/mihaigalos | tr -d '\r')
    [[ ${result} == 'mihaigalos # Mihai Galos' ]] && ok || err "Cannot validate logic."
